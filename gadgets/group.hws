/*
**  Groups.hws
**
**  Class that defines group constructs in RapaGUI
**
**  By Samuel D. Crow
*/

group={}
group["count"]=0
group["prefix$"]="grp_"
group["kind$"]="group"
group["edit$"]=[[<?xml version="1.0" encoding="iso-8859-1"?>
<dialog id="grp_req" title="group">
	<vgroup>
		<radio id="grp_kind">
			<item>Vertical</item>
			<item>Horizontal</item>
			<item>Column</item>
			<item>Scrolling</item>
		</radio>
		<colgroup columns="2">
			<label>ID</label>
			<textentry id="grp_id" reject="0123456789" />
			<label>Title</label>
			<textentry id="grp_title" />
			<label>Columns</label>
			<textentry id="grp_columns" accept="0123456789" />
			<label>Horizontal Alignment</label>
			<choice id="grp_halign">
				<item>Default</item>
				<item>Left</item>
				<item>Center</item>
				<item>Right</item>
			</choice>
			<label>Vertical Alignment</label>
			<choice id="grp_valign">
				<item>Default</item>
				<item>Top</item>
				<item>Center</item>
				<item>Bottom</item>
			</choice>
			<label>Frame Title</label>
			<textentry id="grp_frametitle" />
		</colgroup>
		<checkbox id="grp_frame">Framed</checkbox>
		<checkbox id="grp_same">Uniform Spacing</checkbox>
		<checkbox id="grp_custom">Custom Color</checkbox>
		<popcolor id="grp_color" title="Color" />
		<hgroup>
			<button id="grp_ok" icon="12">OK</button>
			<button id="grp_cancel" icon="13">Cancel</button>
		</hgroup>
	</vgroup>
</dialog>]]

group["define"]={}
group.define["kind$"]=group.kind$
group.define["type"]=group
group.define["group"]=0
group.define["subitems"]=0
group.define["same"]=False
group.define["frame"]=False
group.define["custom_color"]=False
group.define["frametitle$"]=""
group.define["color"]=0
group.define["title$"]=""
group.define["columns"]=2
group.define["halign"]=0
group.define["valign"]=0
group.define["children"]=0
/*
**	Note:  Children of a group have a reserved item called "slot" that holds
**		position data for the parent group
*/

/*
**  Group Factory Method
**
**  Generates new group
**
**  Accepts parent class handle
**
**  Returns initialized table of an empty group
*/
Function group:new(parent)
	local g=CopyTable(self.define, True)

	;add parent and type links
	g["parent"]=parent
	g["application"]=parent.application

	;enumerate count
	g["count"]=self.count
	self.count=self.count+1

	;set default ID
	g["id$"]="grp"..StrStr(g.count)
	g["name$"]=g.id$

	return(g)
EndFunction

/*
**	Group Destructor method
**
**	Removes group and all contents
**
**	Returns boolean value indicating if it aborted
*/
Function group.define:delete()
	Local nested=RawGet(self, "contents")
	If (Not IsNil(nested))
		For Local k=self.children-1 To 0 Step -1 Do nested[k]:delete()
	EndIf
	treeitems[moai.Get(self.id$, "UID")]=Nil
	moai.DoMethod("tree_hierarchy", "remove", self.id$)
	self=nil
EndFunction

/*
**	Add Gadget
**
**	Adds a single gadget to the contents 
*/
Function group.addGadget(dest, type)
	Local data=type:new(dest)
	If (IsNil(RawGet(dest, "contents"))) Then dest["contents"]={}
	dest.contents[dest.children]=data
	data["slot"]=dest.children
	dest.children=dest.children+1
	type.beAdded(data)
EndFunction

/*
**	Remove Gadget
*/
Function group.remGadget(kind$, data)
	Local self=data.parent
	If (self.children>1)
		;shuffle down contents removing item
		self.children=self.children-1
		For Local k=data.slot+1 To self.children
			self.contents[k].slot=k-1
			self.contents[k-1]=self.contents[k]
		Next
		data:delete()
		self.contents[self.children]=Nil
		Return
	EndIf
	data:delete()
	self.children=0
	self.contents=Nil
EndFunction

/*
**	Be Added 
**
**	Add yourself to a parent gadget or window
*/
Function group.beAdded(data)
	moai.DoMethod("tree_hierarchy", "InsertNode", data.id$,
		data.parent.id$, "tail", "vgroup "..data.name$)
	moai.DoMethod("tree_hierarchy", "Open", data.id$)
	Local uid$=moai.Get(data.id$, "UID")
	treeitems[uid$]=data
EndFunction

Function group.define:header$()
	Switch (self.group)
		Case 0:
			Return("vgroup ")
		Case 1:
			Return("hgroup ")
		Case 2:
			Return("colgroup ")
		Case 3:
			Return("scrollgroup ")
	EndSwitch
EndFunction

/*
**  Editor Method
**
**  Creates a graphical requester for RapaGUI group parameters
*/
Function group.editor(data)
	moai.CreateDialog(group.edit$, "win_")
	moai.Set("grp_kind", "Active", data.group)
	moai.Set("grp_columns", "Text", data.columns)
	moai.Set("grp_title", "Text", data.title$)
	moai.Set("grp_frametitle", "Text", data.frametitle$)
	moai.Set("grp_frame", "Selected", data.frame)
	moai.Set("grp_same", "Selected", data.same)
	moai.Set("grp_custom", "Selected", data.custom_color)
	moai.Set("grp_color", "RGB", data.color)
	moai.Set("grp_id", "Text", data.name$)
	moai.Set("grp_halign", "Active", data.halign)
	moai.Set("grp_valign", "Active", data.valign)
	Global ModalUserData=data
	moai.DoMethod("grp_req", "ShowModal")
EndFunction

/*
**	Copy Back
**
**	Copy data back from requestor when OK selected
*/
Function group.CopyBack()
	ModalUserData.group=moai.Get("grp_kind", "Active")
	ModalUserData.columns=moai.Get("grp_columns", "Text")
	ModalUserData.title$=moai.Get("grp_title", "Text")
	ModalUserData.frametitle$=moai.Get("grp_frametitle", "Text")
	ModalUserData.frame=moai.Get("grp_frame", "Selected")
	ModalUserData.same=moai.Get("grp_same", "Selected")
	ModalUserData.custom_color=moai.Get("grp_custom", "Selected")
	ModalUserData.color=moai.Get("grp_color", "RGB")
	ModalUserData.name$=moai.Get("grp_id", "Text")
	ModalUserData.halign=moai.Get("grp_halign", "Active")
	ModalUserData.valign=moai.Get("grp_valign", "Active")
	moai.Set(ModalUserData.id$,
		"Name", ModalUserData:header$()..ModalUserData.name$)
	If (ModalUserData.Title$<>"")
		Local Active$=moai.Get("tree_hierarchy", "Active")
		moai.DoMethod(Active$, "SetItem", 0,
			ModalUserData:Header$()..ModalUserData.Title$)
	EndIf
EndFunction

/*
**	Editor Done Function
**
**	Closes the Requester and cleans up
*/
Function group.editorDone()
	moai.DoMethod("grp_req", "EndModal", 0)
EndFunction

/*
**	Make Body
**
**	Helper function for generating preview XML and export XML
*/
Function group.define:makeBody$(indention, isPreview)
	local xml$=RepeatStr("\t",indention).."<"..self:header$()..
		IIf(self.title$<>""," title=\""..self.title$.."\"","")..
		IIf(self.frame," frame=\"true\" "..
			IIf(self.frametitle$<>"", "frametitle=\""..self.frametitle$.."\" ", ""), "")..
		IIf(self.same, "same=\"true\" ", "")..
		IIf(custom_color, " color=\"#"..RightStr("000000"..self.color, 6).."\" ", "")..
		IIf(isPreview, "", "id=\""..self.id$.."\"")..
		IIf(self.group=2, " columns=\""..self.columns.."\" ", "")
	Switch self.halign
		Case 1:
			xml$=xml$.." halign=\"left\""
		Case 2:
			xml$=xml$.." halign=\"center\""
		Case 3:
			xml$=xml$.." halign=\"right\""
	EndSwitch
	Switch self.valign
		Case 1:
			xml$=xml$.." valign=\"top\""
		Case 2:
			xml$=xml$.." valign=\"center\""
		Case 3:
			xml$=xml$.." valign=\"bottom\""
	EndSwitch
	xml$=xml$..">\n"
	If (Not IsNil(RawGet(self, "contents")))
		For Local k=0 To self.children-1
			Local v=self.contents[k]
			xml$=xml$..IIf(isPreview, v:previewXML$(0), v:generateXML$(indention+1))
		Next
	EndIf
	; Fill unused entries for column view with rectangles
	If (self.group=2)
		Local filler$=RepeatStr("\t", indention+1).."<rectangle />\n"
		xml$=xml$..RepeatStr(filler$, self.children%self.columns)
	EndIf
	xml$=xml$..RepeatStr("\t", indention).."</"..self:header$()..">\n"
	Return(xml$)
EndFunction

/*
**	Generate XML
**
**	Generates XML code for group and all gadgets in it
*/
Function group.define:generateXML$(indention)
	Local xml$=self:makeBody$(indention, False)
	Return(xml$)
EndFunction

/*
**	Preview XML
**
**	Generates XML preview for group and all gadgets in it
*/
Function group.define:previewXML$(indention)
	Local xml$=self:makeBody$(0, True)
	Return(xml$)
EndFunction

/*
**	Save
**
**	Strip out invariants for saving buffer
*/
Function group.save(self)
	self.parent=Nil
	self.application=Nil
	self.type=Nil
	self.header$=Nil
	self.id$=Nil
	self.count=Nil
	If self.children>0
		For k=0 To self.children-1
			Local v=p_CloneFields(self.contents[k])
			v.type.save(v)
			self.contents[k]=v
		Next
	EndIf
EndFunction

/*
**	Load
**
**	Add all missing invariant fields to load buffer
*/
Function group.load(self, myParent)
	self["count"]=group.count
	self["id$"]="grp"..StrStr(self.count)
	group.count=group.count+1
	self["makeBody$"]=group.define.makeBody$
	self["previewXML$"]=group.define.previewXML$
	self["generateXML$"]=group.define.generateXML$
	self["delete"]=group.define.delete
	self["parent"]=myParent
	self["application"]=myParent.application
	self["type"]=group
	self["header$"]=group.define.header$
	group.beAdded(self)
	If Not IsNil(RawGet(self, "contents"))
		For k, v In IPairs(self.contents)
			v["slot"]=k
			gadgetclasses[v.kind$].load(v, self)
		Next
	EndIf
EndFunction
