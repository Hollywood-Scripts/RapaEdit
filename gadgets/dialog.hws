/*
**  Dialog.hws
**
**  A dialog gadget type for RapaEdit
**
**  By Samuel D. Crow
*/
Global dialog={}
dialog["count"]=0
dialog["prefix$"]="dlg_"
dialog["kind$"]="dialog"
dialog["edit$"]=[[<?xml version="1.0" encoding="iso-8859-1"?>
<dialog id="dlg_req" title="dialog">
	<vgroup>
		<colgroup columns="2">
			<label>ID</label>
			<textentry id="dlg_id" reject="0123456789" />
			<label>Title</label>
			<textentry id="dlg_title" />
			<label>Width</label>
			<textentry id="dlg_width" accept="1234567890" />
			<label>Height</label>
			<textentry id="dlg_height" accept="1234567890" />
		</colgroup>
		<checkbox id="dlg_size">Resizable</checkbox>
		<checkbox id="dlg_drag">Draggable</checkbox>
		<checkbox id="dlg_close">Closable</checkbox>
		<hgroup>
			<button id="dlg_ok" icon="12">OK</button>
			<button id="dlg_cancel" icon="13">Cancel</button>
		</hgroup>
	</vgroup>
</dialog>]]

dialog["define"]={}
dialog.define["width"]=800
dialog.define["height"]=600
dialog.define["resizable"]=True
dialog.define["closegadget"]=True
dialog.define["draggable"]=True
;dialog.define.contents must be Nil if empty
;parent, count and id defined by factory method

/*
**  Dialog Factory Method
**
**	Generates dialog defines and update static counter
**
**	Accepts parent application class
**
**  Returns initialized table of dialog data
*/
Function dialog:new(app)
	Local w=CopyTable(self.define)

	; add parent and type link
	w["parent"]=app
	w["application"]=app.application
	w["type"]=dialog
	w["kind$"]=dialog.kind$

	;enumerate count from static variable
	w["count"]=self.count
	self.count=w.count+1

	;set default id
	w["id$"]="dlg"..StrStr(w.count)
	w["title$"]=w.id$
	w["name$"]=w.id$

	Return(w)
EndFunction

/*
**	Dialog destructor method
**
**	Deletes dialog and all contents 
*/
Function dialog.define:delete()
	If (Not IsNil(RawGet(self, "contents"))) Then self.contents:delete()
	Local uid$=moai.Get(self.id$, "UID")
	treeitems[uid$]=Nil
	moai.DoMethod("tree_hierarchy", "Remove", self.id$)
	self=Nil
EndFunction

/*
**	Add Gadget
**
**	Adds a single gadget to the contents 
**
**	Note: To contain multiple gadgets a group must be added around it.
*/
Function dialog.addGadget(dest, type)
	If (IsNil(RawGet(dest, "contents")))
		Local data=type:new(dest)
		;type.editor(data)
		dest["contents"]=data
		type.beAdded(data)
	Else
		moai.Request("Need Group",
			"Dialog contents is already occupied.\nCOnsider grouping the contents first.",
			"OK", "Error")
	EndIf
EndFunction

/*
**	Remove Gadget
**
**	Removes group from contents
*/
Function dialog.remGadget(kind$, data)
	If Not IsNil(RawGet(data, "contents")) And moai.Request("Deleting group", 
		"Do you really want to delete the group?\nAll contents will be deleted also.",
		"Yes|No", "Warning")>0
		data.contents:delete()
	EndIf
EndFunction

/*
**	Be Added
*/
Function dialog.beAdded(data)
	moai.DoMethod("tree_hierarchy", "InsertNode", data.id$,
		data.parent.id$, "tail", "dialog "..data.name$)
	moai.DoMethod("tree_hierarchy", "Open", data.id$)
	Local uid$=moai.Get(data.id$, "UID")
	treeitems[uid$]=data
EndFunction

/*
**  Editor Method
**
**  Creates a graphical requester for RapaGUI dialog parameters
**
**  Note: Don't call on dialog.define or it will change the default values
**      and those defaults won't be saved with the document.  Instead call
**      this method on local copies created by the dialog:new() factory.
*/
Function dialog.editor(data)
	;pass the data pointer to the static handler routine
	moai.CreateDialog(dialog.edit$, "win_")
	moai.Set("dlg_id", "Text", data.name$)
	moai.Set("dlg_title", "Text", data.title$)
	moai.Set("dlg_width", "Text", data.width)
	moai.Set("dlg_height", "Text", data.height)
	moai.Set("dlg_size", "Selected", data.resizable)
	moai.Set("dlg_drag", "Selected", data.draggable)
	moai.Set("dlg_close", "Selected", data.closegadget)
	Global ModalUserData=data
	moai.DoMethod("dlg_req", "ShowModal")
EndFunction

/*
**	CopyBack
**
**	Processes buffer when OK pressed
*/
Function dialog.CopyBack()
	ModalUserData.name$=moai.Get("dlg_id", "Text")
	ModalUserData.title$=moai.Get("dlg_title", "Text")
	ModalUserData.width=moai.Get("dlg_width", "Text")
	ModalUserData.height=moai.Get("dlg_height", "Text")
	ModalUserData.resizable=moai.Get("dlg_size", "Selected")
	ModalUserData.draggable=moai.Get("dlg_drag", "Selected")
	ModalUserData.closegadget=moai.Get("dlg_close", "Selected")
	moai.Set(ModalUserData.id$, "Name", "dialog "..ModalUserData.name$)
EndFunction

/*
**	EditorDone Method
**
**	Called by handler method when OK or Cancel have been clicked
*/
Function dialog.editorDone()
	moai.DoMethod("dlg_req", "EndModal", 0)
EndFunction


/*
**	Make Body
**
**	Helper function for generating preview XML and export XML
*/
Function dialog.define:makeBody$()
	Return(" title=\""..self.title$.."\" width=\""..self.width..
		"\" height=\""..self.height.."\" dragbar=\""..
		IIf(self.draggable,"True","False").."\" closegadget=\""..
		IIf(self.closegadget,"True","False").."\" >\n")
EndFunction

/*
**	Generate XML
**
**	Recursively generates XML output for this item and all itmes contained in it
*/
Function dialog.define:generateXML$()
	Return("\t<dialog id=\""..self.id$.."\""..self:makeBody$()..
		IIf(HaveItem(self, "contents"), self.contents:generateXML$(2), "")..
		"\t</dialog>\n")
EndFunction

/*
**	Preview XML
**
**	Recursively generates XML preview for this item and all itmes contained in it
*/
Function dialog.define:previewXML$()
	Return("\t<dialog id=\"pre_view\""..self:makeBody$().."<vgroup>\n"..
		IIf(HaveItem(self, "contents"), self.contents:previewXML$(0), "")..
		"<button id=\"pre_exit\">Exit Preview</button>\n</vgroup>\n</dialog>\n")
EndFunction

/*
**	Save
**
**	Strip out invariants for saving buffer
*/
Function dialog.save(self)
	self.parent=Nil
	self.application=Nil
	self.type=Nil
	self.id$=Nil
	self.count=Nil
	If (Not IsNil(RawGet(self, "contents")))
		Local buf=p_CloneFields(self.contents)
		buf.type.save(buf)
		self.contents=buf
	EndIf
EndFunction

/*
**	Load
**
**	Add all missing invariant fields to load buffer
*/
Function dialog.load(self, myParent)
	self["count"]=dialog.count
	self["id$"]="dlg"..StrStr(self.count)
	dialog.count=dialog.count+1
	self["previewXML$"]=dialog.define.previewXML$
	self["generateXML$"]=dialog.define.generateXML$
	self["makeBody$"]=dialog.define.makeBody$
	self["delete"]=dialog.define.delete
	self["parent"]=myParent
	self["application"]=myParent
	self["type"]=dialog
	dialog.beAdded(self)
	If Not IsNil(RawGet(self, "contents"))
		gadgetclasses[self.contents.kind$].load(self.contents, self)
	EndIf
EndFunction
